# Copyright 1999-2022 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

inherit meson toolchain-funcs xdg-utils

DESCRIPTION="A window switcher, run dialog and dmenu replacement"
HOMEPAGE="https://github.com/lbonn/rofi"
SRC_URI="https://github.com/lbonn/rofi/releases/download/${PV}+wayland1/${P}+wayland1.tar.xz -> ${P}.tar.xz"

LICENSE="MIT"
SLOT="0"
KEYWORDS="~amd64 ~arm64 ~x86"
IUSE="+drun test wayland +window xcb"
RESTRICT="mirror !test? ( test )"
REQUIRED_USE="^^ ( xcb wayland )"
S="${WORKDIR}/${P}+wayland1"

BDEPEND="
	sys-devel/bison
	sys-devel/flex
	virtual/pkgconfig
"
RDEPEND="
	dev-libs/glib:2
	x11-libs/cairo
	x11-libs/gdk-pixbuf:2
	x11-libs/libxkbcommon
	x11-libs/pango
	wayland? (
		dev-libs/wayland
		dev-libs/wayland-protocols
	)
	xcb? (
		x11-libs/cairo[X]
		x11-libs/libxcb:=
		x11-libs/startup-notification
		x11-libs/xcb-util
		x11-libs/xcb-util-cursor
		x11-libs/xcb-util-wm
		x11-misc/xkeyboard-config
	)
"
DEPEND="
	${RDEPEND}
	xcb? ( x11-base/xorg-proto )
	test? ( >=dev-libs/check-0.11 )
"

src_prepare() {
	default
	cp ${FILESDIR}/meson_build_manpages.sh ${S}/doc
	chmod a+x ${S}/doc/meson_build_manpages.sh
}
src_configure() {
	tc-export CC

	local emesonargs=(
		$(meson_use drun)
		$(meson_feature test check)
		$(meson_use window)
		$(meson_feature wayland)
		$(meson_feature xcb)
	)
	meson_src_configure
}

pkg_postinst() {
	for v in ${REPLACING_VERSIONS}; do
		if ver_test "${v}" -lt 1.7.0; then
			elog "Rofi 1.7.0 removed the (deprecated) xresources based configuration setup."
			elog "If you are still using old configuration setup, please convert it to new format manually."
			elog "The new format configuration can be generated by 'rofi -dump-config > ~/.config/rofi/config.rasi'."
			elog "For more information, please see https://github.com/davatorium/rofi/releases/tag/1.7.0"
		fi
	done

	xdg_icon_cache_update
}

pkg_postrm() {
	xdg_icon_cache_update
}
